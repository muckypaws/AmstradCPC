10 ' AYO A game for
20 ' 1 or 2 Players
30 '
40 ' by Daniel J. Bishop
50 ' CPC Only
60 ' (c) Computing with the Amstrad
70 ' Typed in by J.Brooks
80 MODE 1:BORDER 0:INK 0,0:INK 1,24:INK 2,11:INK 3,3
90 ENV 1,20,0,1,15,-1,2
100 SYMBOL AFTER 125
110 SYMBOL 125,0,0,0,0,0,1,7,15
120 SYMBOL 126,0,0,0,1,195,230,252,252
130 SYMBOL 127,0,0,0,128,192,192,64,0
140 SYMBOL 128,0,0,0,0,2,1,2,4
150 SYMBOL 129,0,0,24,103,18,255,161,2
160 SYMBOL 130,0,70,168,170,82,164,116,88
170 SYMBOL 131,31,31,14,14,12,4,4,4
180 SYMBOL 132,252,252,56,56,16,16,16,16
190 SYMBOL 133,0,0,48,48,96,248,240,216
200 SYMBOL 134,0,0,48,48,24,252,60,108
210 SYMBOL 135,192,192,96,96,224,96,96,240
220 SYMBOL 136,152,248,92,24,28,60,62,255
230 SYMBOL 137,0,0,0,0,0,0,3,3
240 SYMBOL 221,170,85,42,21,26,37,66,129
250 DEFINT a-z:DIM hp(5),h(1,5),ht(1,5),hxy(1,5,1),txy(1,1),score(1),sxy(1,1),seed(1),hnum(1,5,1)
260 n0=160:n1=10:FOR l0=0 TO 5:hxy(0,l0,0)=n0:hxy(0,l0,1)=104:hxy(1,l0,0)=n0:hxy(1,l0,1)=248:hnum(1,l0,0)=n1:hnum(1,l0,1)=7:hnum(0,l
0,0)=n1:hnum(0,l0,1)=22:n0=n0+64:n1=n1+4:NEXT
270 rh=32:sxy(1,0)=80:sxy(1,1)=160:sxy(0,0)=560:sxy(0,1)=192:rsh=45
280 txy(0,0)=33:txy(0,1)=17:txy(1,0)=4:txy(1,1)=12:l$=CHR$(10)+CHR$(13):mesx=16:mesy=14:comp=-1:board=0
290 GOSUB 720
300 FOR l0=0 TO 1:score(l0)=0:NEXT
310 turn=0
320 GOSUB 880:board=-1:FOR l0=0 TO 1:FOR l1=0 TO 5:h(l0,l1)=4:NEXT:seed(l0)=0:NEXT:GOSUB 940
330 PEN 1:LOCATE mesx,mesy:PRINT"Start Game":LOCATE mesx,mesy:tm=2:GOSUB 1210:PEN 3:PRINT STRING$(10,CHR$(143)):GOSUB 1250
340 GOSUB 990
350 GOSUB 1130
360 IF comp AND turn=1 THEN GOSUB 1280:GOTO 460
370 user$=INKEY$:IF user$="" THEN 370
380 user$=LOWER$(user$)
390 IF user$="c" THEN comp=NOT comp:GOTO 340
400 IF user$="s" THEN 680
410 IF user$="r" THEN score((turn+1)MOD 2)=score((turn+1)MOD 2)+(48-seed(turn)):score(turn)=score(turn)+seed(turn):GOTO 680
420 IF user$="i" THEN GOSUB 1650:GOSUB 720:board=0:GOSUB 880:board=-1:GOSUB 940:GOSUB 990:GOSUB 1130:GOTO 370
430 house=VAL(user$):IF house=0 OR house>6 THEN 370
440 house=house-1
450 IF h(turn,house)=0 THEN 370
460 GOSUB 1150:x0=hxy(turn,house,0):y0=hxy(turn,house,1):r0=rh:GOSUB 970:n2=h(turn,house):h(turn,house)=0
470 n0=turn:n1=house:FOR l0=1 TO n2
480 IF n0=0 THEN n1=n1+1:IF n1=6 THEN n0=1:n1=5:GOTO 500
490 IF n0=1 THEN n1=n1-1:IF n1=-1 THEN n0=0:n1=0
500 h(n0,n1)=h(n0,n1)+1
510 GOSUB 1060
520 GOSUB 1180:tm=0.5:GOSUB 1210
530 NEXT
540 IF h(n0,n1)<2 OR h(n0,n1)>3 OR n0=turn THEN 610
550 WHILE n0<>turn AND h(n0,n1)<=3
560 IF h(n0,n1)=1 THEN 580
570 seed(turn)=seed(turn)+h(n0,n1):x0=hxy(n0,n1,0):y0=hxy(n0,n1,1):r0=rh:GOSUB 1150:GOSUB 970:FOR l0=1 TO h(n0,n1):n7=turn:n6=n1:n1=
-1:GOSUB 1060:n1=n6:GOSUB 1180:tm=0.3:GOSUB 1210:NEXT:h(n0,n1)=0
580 IF n0=0 THEN n1=n1-1:IF n1=-1 THEN n0=0:n1=0:GOTO 600
590 IF n0=1 THEN n1=n1+1:IF n1=6 THEN n0=0:n1=5
600 WEND
610 turn=(turn+1) MOD 2
620 n0=0:FOR l0=0 TO 5:n0=n0+h(turn,l0):NEXT
630 n1=0:FOR l0=0 TO 5:n1=n1+h((turn+1) MOD 2,l0):NEXT
640 IF n0=0 GOTO 670
650 IF no<=1 AND n1<=1 THEN 670
660 GOTO 340
670 FOR l0=0 TO 1:score(l0)=score(l0)+seed(l0):NEXT
680 PEN 1:LOCATE mesx,mesy,:PRINT"GAME OVER":GOSUB 1250:LOCATE mesx,mesy:tm=2:GOSUB 1210:PEN 3:PRINT STRING$(9,CHR$(143))
690 IF user="s" THEN 300
700 GOTO 310
710 ' Draw screen display
720 PEN 1:PRINT TAB(7)CHR$(219)l$TAB(6)CHR$(222)CHR$(219)l$TAB(5)CHR$(217)CHR$(220)CHR$(219)l$TAB(4)CHR$(222)CHR$(217)" "CHR$(219)l$
TAB(4)CHR$(207)CHR$(217)CHR$(222)CHR$(219)l$" "CHR$(222)CHR$(207)CHR$(207)CHR$(217)CHR$(220)
730 PRINT CHR$(222)CHR$(223)" "CHR$(7)CHR$(217)l$CHR$(207)CHR$(207)CHR$(223)CHR$(220)l$CHR$(207)CHR$(221)CHR$(220)l$CHR$(207)l$CHR$(
220)
740 PEN 0:LOCATE 5,3:PRINT CHR$(22)CHR$(1)CHR$(212):LOCATE 7,5:PRINT CHR$(214):LOCATE 7,1:PRINT CHR$(212):LOCATE 5,7:PRINT CHR$(214)
750 PEN 2:LOCATE 12,3:PRINT"The Game Of Africa"
760 PEN 2:LOCATE 34,3:PRINT STRING$(6,CHR$(143)):PEN 1:LOCATE 34,4:PRINT STRING$(6,CHR$(143))
770 PEN 3:LOCATE 34,3:PRINT CHR$(125)CHR$(126)CHR$(127)CHR$(128)CHR$(129)CHR$(130)
780 LOCATE 34,4:PRINT CHR$(131)CHR$(132)CHR$(137)"  "CHR$(136)
790 PEN 0:LOCATE 36,4:PRINT CHR$(133)CHR$(134)CHR$(135)
800 PRINT CHR$(22)CHR$(0)
810 PEN 3:FOR l0=0 TO 7:LOCATE 8-l0,6+l0:PRINT CHR$(214)STRING$(l0,CHR$(143))STRING$(24,CHR$(143))STRING$(l0,CHR$(143))CHR$(215):NEX
T
820 LOCATE 1,14:PRINT STRING$(80,CHR$(143))
830 FOR l0=0 TO 7:LOCATE l0+1,l0+16:PRINT CHR$(213)STRING$(7-l0,CHR$(143))STRING$(24,CHR$(143))STRING$(7-l0,CHR$(143))CHR$(212):NEXT
840 PEN 2:FOR l0=0 TO 1:FOR l1=0 TO 5:LOCATE hnum(l0,l1,0)+1,hnum(l0,l1,1):PRINT USING"#";l1+1:NEXT:NEXT
850 PEN 1:LOCATE 1,25:PRINT"I";:PEN 2:PRINT"nstructions";:PEN 1:PRINT" C";:PEN 2:PRINT"omputer";:PEN 1:PRINT" R";:PEN 2:PRINT"esign"
;:PEN 1:PRINT" S";:PEN 2:PRINT"tart-Again";
860 RETURN
870 ' Empty houses
880 FOR l0=0 TO 1:FOR l1=0 TO 5:IF h(l0,l1)>0 OR NOT board THEN x0=hxy(l0,l1,0):y0=hxy(l0,l1,1):r0=rh:GOSUB 970
890 NEXT:NEXT
900 FOR l0=0 TO 1:x0=sxy(l0,0):y0=sxy(l0,1):r0=rsh:GOSUB 970
910 NEXT
920 RETURN
930 ' Fill houses
940 FOR l0=0 TO 1:FOR l1=0 TO 5:l2=1:WHILE l2<=h(l0,l1):l2=l2+1:n0=l0:n1=l1:GOSUB 1060:WEND:NEXT:l2=1:WHILE l2<=seed(l0):l2=l2+1:n7=
l0:n1=-1:GOSUB 1060:WEND:NEXT
950 RETURN
960 ' Draw a house
970 FOR x1=-r0 TO r0 STEP 2:y1=SQR(r0^2-x1^2):MOVE x0+x1,y0+y1:DRAW x0+x1,y0-y1,0:NEXT:RETURN
980 ' Update score panels
990 FOR l0=0 TO 1
1000 LOCATE txy(l0,0),txy(l0,1):IF turn=l0 THEN PEN 1:PRINT CHR$(227); ELSE PEN 3:PRINT CHR$(227);
1010 PEN 2:PRINT USING"####";score(l0)
1020 NEXT
1030 LOCATE 3,12:IF comp THEN PRINT"C" ELSE PAPER 3:PRINT" ":PAPER 0
1040 RETURN
1050 ' Seed
1060 IF n1 THEN MOVE sxy(n7,0),sxy(n7,1):n2=RND(1)*rsh*0.7:n3=RND(1)*rsh*0.7
1070 IF n1<>-1 THEN MOVE hxy(n0,n1,0),hxy(n0,n1,1):n2=RND(1)*rh*0.5:n3=RND(1)*rh*0.5
1080 IF RND(1)<0.5 THEN n4=-1 ELSE n4=1
1090 IF RND(1)<0.5 THEN n5=-1 ELSE n5=1
1100 PLOTR n2*n4,n3*n5+2,1:MOVER -1,-1:DRAWR 2,0:MOVER -3,-1:DRAWR 4,0:MOVER -3,-1:DRAWR 2,0:PLOTR -1,-1
1110 RETURN
1120 ' Clear keyboard buffer
1130 s0$=INKEY$:IF s0$="" THEN RETURN ELSE 1130
1140 ' Scrunch
1150 SOUND 4,0,48,15,1,0,30
1160 RETURN
1170 ' Tok
1180 SOUND 4,0,1,15,0,0,30
1190 RETURN
1200 ' Pause
1210 tm!=tm!*300:n0!=TIME
1220 WHILE n0!+tm!>TIME:WEND
1230 RETURN
1240 ' Bell
1250 SOUND 4,30,48,15,1
1260 RETURN
1270 ' Computer move algorithm
1280 FOR l0=0 TO 5:hp(l0)=0:NEXT
1290 FOR l0=0 TO 5
1300 IF h(1,l0)=0 THEN hp(l0)=-1000:GOTO 1570
1310 FOR l1=0 TO 1:FOR l2=0 TO 5
1320 ht(l1,l2)=h(l1,l2)
1330 NEXT:NEXT
1340 n1=l0:n2=1:n0=ht(1,l0):ht(1,l0)=0
1350 FOR l1=1 TO n0
1360 IF n2=1 THEN n1=n1-1:IF n1=-1 THEN n2=0:n1=0:GOTO 1380
1370 IF n2=0 THEN n1=n1+1:IF n1=6 THEN n2=1:n1=5
1380 ht(n2,n1)=ht(n2,n1)+1
1390 NEXT
1400 IF (ht(n2,n1)=2 OR ht(n2,n1)=3) AND n2=0 THEN n0=-1 ELSE n0=0
1410 FOR l1=0 TO 11
1420 IF ht(n2,n1)>3 THEN n0=0
1430 IF n2=0 THEN 1480
1440 IF ht(n2,n1)=0 THEN hp(l0)=hp(l0)+3:GOTO 1540
1450 IF ht(n2,n1)=1 THEN hp(l0)=hp(l0)-6:GOTO 1540
1460 IF ht(n2,n1)=2 THEN hp(l0)=hp(l0)-9:GOTO 1540
1470 IF ht(n2,n1)>=3 THEN hp(l0)=hp(l0)+6:GOTO 1540
1480 IF ht(n2,n1)<2 THEN 1540
1490 IF ht(n2,n1)=2 AND NOT n0 THEN hp(l0)=hp(l0)+5:GOTO 1540
1500 IF ht(n2,n1)=2 AND n0 THEN hp(l0)=hp(l0)+60:GOTO 1540
1510 IF ht(n2,n1)=3 AND NOT n0 THEN hp(l0)=hp(l0)-6:GOTO 1540
1520 IF ht(n2,n1)=3 AND n0 THEN hp(l0)=hp(l0)+90:GOTO 1540
1530 IF ht(n2,n1)>3 THEN hp(l0)=hp(l0)-7
1540 IF n2=1 THEN n1=n1+1:IF n1=6 THEN n2=0:n1=5:GOTO 1560
1550 IF n2=0 THEN n1=n1-1:IF n1=-1 THEN n2=1:n1=0
1560 NEXT
1570 NEXT
1580 house=0:n0=hp(0)
1590 FOR l0=1 TO 5:IF hp(l0)>n0 THEN n0=hp(l0):house=l0
1600 NEXT
1610 FOR l0=0 TO 5:IF hp(l0)=hp(house) THEN IF RND(1)<0.5 THEN house=l0
1620 NEXT
1630 RETURN
